plugins {
    id 'java'
    id 'maven-publish'
    id 'org.quiltmc.gradle.licenser' version '1.1.1'
}

version '1.0.2'
group 'org.quiltmc'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

version = version + (System.getenv("GITHUB_ACTIONS") ? "" : "+local")

repositories {
    mavenCentral()
    maven {
        name 'Quilt Repository'
        url 'https://maven.quiltmc.org/repository/release'
    }
}

dependencies {
    implementation "org.ow2.asm:asm:9.3"
    implementation "org.ow2.asm:asm-commons:9.3"
    implementation "org.ow2.asm:asm-tree:9.3"
    implementation "org.ow2.asm:asm-util:9.3"
    implementation "cuchaz:enigma:${enigma_version}"

    testImplementation "cuchaz:enigma-swing:${enigma_version}"
}

license {
    rule file('codeformat/FABRIC_MODIFIED_HEADER')
    rule file('codeformat/HEADER')

    include '**/*.java'
}

java {
    withSourcesJar()
}

publishing {
    def ENV = System.getenv()
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }

    repositories {
        if (ENV.MAVEN_URL) {
            maven {
                url = ENV.MAVEN_URL

                credentials {
                    username = ENV.MAVEN_USERNAME
                    password = ENV.MAVEN_PASSWORD
                }
            }
        }
        mavenLocal()
    }
}
